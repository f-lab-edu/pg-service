import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

group = 'io.github.gugbab2'
version = '0.0.1-SNAPSHOT'
description = 'SRT 클론 서비스 프로젝트'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(Versions.java)
    }
}

subprojects {
    apply plugin: Plugins.java
    apply plugin: Plugins.springBoot
    apply plugin: Plugins.springDependencyManagement

    group = rootProject.group
    version = rootProject.version
    sourceCompatibility = Versions.java

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom(SpringBootPlugin.BOM_COORDINATES)
        }
    }

    dependencies {
        // Spring Boot
        implementation Libs.springBootStarterWeb
        implementation Libs.springBootStarterValidation

        // Lombok
        compileOnly Libs.lombok
        annotationProcessor Libs.lombok

        // Test
        testImplementation Libs.springBootStarterTest
        testImplementation Libs.mockitoCore
        testRuntimeOnly Libs.junitPlatformLauncher
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
